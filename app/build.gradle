apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

ext {
    kycVersion = "3.0.1"
    repoUrl = project.hasProperty("kycRepoUrl") ? project.property("kycRepoUrl") : "https://dl.bintray.com/sumsub/maven"
}

repositories {
    mavenCentral()
    maven {
        url project.ext.repoUrl
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sumsub.auth_sdk"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 5
        versionName project.ext.kycVersion

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('../keys/release.jks')
            storePassword 'sumsub'
            keyAlias = 'sumsub'
            keyPassword 'sumsub'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField("String", 'BASE_URL', '"msdk.sumsub.com"')
        }

        debug {
            buildConfigField("String", 'BASE_URL', '"test-msdk.sumsub.com"')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def formattedDate = new Date().format('yyyy-MM-dd')
            outputFileName = "kyc-release-v" + versionName + "-" + formattedDate + ".apk"
        }
    }

    packagingOptions {
        exclude 'META-INF/proguard/coroutines.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.android.volley:volley:1.1.0'

    implementation "com.sumsub:kyc-core:${project.ext.kycVersion}"
    implementation "com.sumsub:kyc-liveness3d:${project.ext.kycVersion}"
    implementation "com.sumsub:kyc-client:${project.ext.kycVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
